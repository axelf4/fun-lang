grammar<'input>(db: &dyn crate::Db);

use crate::lexer::{self, Token};
use crate::ast::{Id, Term, Definition};
use crate::core::Icitness::*;

extern {
	type Location = usize;
	type Error = lexer::Error<'input>;

	enum Token<'input> {
		"\\" => Token::BackSlash,
		"->" => Token::Arrow,
		"(" => Token::Lparen,
		")" => Token::Rparen,
		"{" => Token::Lbrace,
		"}" => Token::Rbrace,
		"=" => Token::Equals,
		":" => Token::Colon,
		"let" => Token::Let,
		"in" => Token::In,
		"case" => Token::Case,
		"of" => Token::Of,
		"@{" => Token::LayoutStart,
		"," => Token::LayoutSep,
		"}@" => Token::LayoutEnd,
		Num => Token::Number(<i32>),
		Ident => Token::Ident(<&'input str>),
		"Type" => Token::Type,
	}
}

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

Id: Id = {
	Ident => Id::new(db, <>.to_string()),
}

Binding: (Id, Option<Box<Term>>, Box<Term>) = {
	<x:Id> "=" <e:Term> => (x, None, e),
	<x:Id> ":" <t:Term> "=" <e:Term> => (x, Some(t), e),
}

pub Term: Box<Term> = {
	Term1,
	"\\" <x:Id> "->" <e:Term1> => Box::new(Term::Abs(Explicit, x, e)),

	"(" <x:Id> ":" <a:Term> ")" "->" <b:Term> => Box::new(Term::Pi(<>)),

	"case" <_x:Term> "of" "@{" Comma<Arm> "}@" => Box::new(Term::Number(69)),

	"let" "@{" <bs:Comma<Binding>> "}@" "in" <e:Term> => {
		bs.into_iter().rev().fold(e, |acc, (x, _t, v)| Box::new(Term::App(Box::new(Term::Abs(Explicit, x, acc)), Explicit, v)))
	},
};

Term1: Box<Term> = {
	Term2,
	<a:Term1> <b:Term2> => Box::new(Term::App(a, Explicit, b)),
	<a:Term1> "{" <b:Term> "}" => Box::new(Term::App(a, Implicit, b)),
}

Term2: Box<Term> = {
	Num => Box::new(Term::Number(<>)),
	Id => Box::new(Term::Var(<>)),
	"Type" => Box::new(Term::Type),
	"(" <Term> ")",
};

Arm: () = {
	Term "->" Term => {},
};

Definition: Definition = {
	<Binding> => Definition::Constant { name: <>.0, ty: <>.1.map(|x| *x), value: *<>.2 },
};

pub Program = Comma<Definition>;
